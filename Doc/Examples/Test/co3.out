gap> x:=Indeterminate(GF(2));
x_1
gap> SetName(x,"x");
gap> f:=x^23-1;
x^23+Z(2)^0
gap> Factors(f);
[ x+Z(2)^0, x^11+x^9+x^7+x^6+x^5+x+Z(2)^0, x^11+x^10+x^6+x^5+x^4+x^2+Z(2)^0 ]
gap> f:=First(Factors(f),i->Degree(i)>1);
x^11+x^9+x^7+x^6+x^5+x+Z(2)^0
gap> LoadPackage("guava");
true
gap> cod:=GeneratorPolCode(f,23,GF(2));
a cyclic [23,12,1..7]3 code defined by generator polynomial over GF(2)
gap> IsPerfectCode(cod);
true
gap> ext:=ExtendedCode(cod);
a linear [24,12,8]4 extended code
gap> WeightDistribution(ext);
[ 1, 0, 0, 0, 0, 0, 0, 0, 759, 0, 0, 0, 2576, 0, 0, 0, 759, 0, 0, 0, 0, 0, 0, 
  0, 1 ]
gap> autext:=AutomorphismGroup(ext);
<permutation group of size 244823040 with 11 generators>
gap> gens:=SmallGeneratingSet(autext);;
gap> m24:=Group(gens);
Group([ (1,7,11,12,16,15,8,6,14,24,23,18,20,9,5,17,22,2,13,4,21)(3,19,10), 
  (1,12,20,18,14,10,11)(2,3,24,9,6,21,16,23,7,22,5,15,4,17)(13,19) ])
gap> SetName(m24,"m24");
gap> Size(m24);
244823040
gap> Transitivity(m24,[1..24]);
5
gap> st:=Stabilizer(m24,[23,24],OnSets);
<permutation group of size 887040 with 9 generators>
gap> gens:=SmallGeneratingSet(st);;
gap> st2:=Group(gens);
Group([ (1,3,17)(2,7,4,9,20,10)(5,22)(6,19,14,8,13,16)(11,12,21)(23,24), 
  (1,5,8,15,16,17)(2,13,21)(3,19)(4,7)(6,10,11)(9,22,14,20,18,12) ])
gap> m22a:=Action(st2,[1..22]);
Group([ (1,3,17)(2,7,4,9,20,10)(5,22)(6,19,14,8,13,16)(11,12,21), 
  (1,5,8,15,16,17)(2,13,21)(3,19)(4,7)(6,10,11)(9,22,14,20,18,12) ])
gap> SetName(m22a,"m22a");
gap> Size(m22a);
887040
Order   Index  Structure  G.2       Abstract   Mathieu
 5760      77  2^4:A_6    2^4:S_6   N(2A^4)    hexad
gap> s:=SylowSubgroup(m22a,2);;
gap> pciso:=IsomorphismPcGroup(s);
Pcgs([ (1,20)(3,14)(6,10)(7,18)(9,19)(12,15)(16,17), 
  (1,20)(3,6)(4,13)(5,21)(9,17)(10,14)(16,19), 
  (3,6)(4,9)(5,16)(8,11)(12,18)(13,17)(19,21), 
  (4,19)(5,17)(7,15)(8,11)(9,21)(10,14)(13,16), 
  (2,13)(3,16)(4,8)(5,11)(6,9)(10,19)(14,17)(21,22), 
  (2,14)(3,8)(4,16)(5,9)(6,11)(10,22)(13,17)(19,21), 
  (2,3)(4,17)(5,19)(6,22)(8,14)(9,21)(10,11)(13,16), 
  (2,22)(3,6)(4,5)(8,11)(9,16)(10,14)(13,21)(17,19) ]) -> 
[ f1, f2, f3, f4, f5, f6, f7, f8 ]
gap> a:=Image(pciso);
Group([ f1, f2, f3, f4, f5, f6, f7, f8 ])
gap> n:=Filtered(NormalSubgroups(a),i->Size(i)=16
> and IsElementaryAbelian(i));
[ Group([ f2, f3*f4, f6*f7, f8 ]), Group([ f1*f2, f3*f4, f6*f7, f8 ]), 
  Group([ f1, f3*f4, f6*f7, f8 ]), Group([ f5, f6*f7, f7, f8 ]), 
  Group([ f2*f3*f4, f6*f7, f7, f8 ]) ]
gap> n:=List(n,i->PreImage(pciso,i));;
gap> e:=Filtered(n,i->IsRegular(i,MovedPoints(i)));;Length(e);
1
gap> e:=e[1];;
gap> h:=Normalizer(m22a,e);;
gap> ophom:=ActionHomomorphism(m22a,RightCosets(m22a,h),OnRight);;
gap> mop:=Image(ophom);
<permutation group with 2 generators>
gap> DegreeAction(mop);
77
gap> dp:=DirectProduct(m22a,mop);;
gap> emb1:=Embedding(dp,1);;
gap> emb2:=Embedding(dp,2);;
gap> diag:=List(GeneratorsOfGroup(m22a), 
> i->Image(emb1,i)*Image(emb2,Image(ophom,i)));;
gap> diag:=Group(diag,());;
gap> SetName(diag,"M22.2-99");
gap> LoadPackage("grape");
true
gap> gamma:=NullGraph(diag,100);
rec( isGraph := true, order := 100, group := M22.2-99, 
  schreierVector := [ -1, 2, 1, 1, 2, 1, 1, 2, 2, 1, 1, 2, 1, 1, 2, 1, 1, 2, 
      2, 2, 2, 1, -2, 2, 2, 1, 2, 1, 1, 1, 1, 1, 2, 1, 2, 2, 1, 1, 1, 1, 1, 
      2, 2, 1, 1, 1, 1, 2, 1, 2, 1, 2, 2, 1, 1, 2, 2, 2, 1, 2, 1, 2, 1, 1, 1, 
      2, 1, 1, 2, 1, 1, 2, 1, 2, 1, 2, 1, 1, 1, 2, 2, 2, 2, 1, 2, 2, 1, 2, 1, 
      1, 2, 2, 2, 1, 2, 1, 2, 1, 1, -3 ], adjacencies := [ [  ], [  ], [  ] ],
  representatives := [ 1, 23, 100 ], isSimple := true )
gap> AddEdgeOrbit(gamma,[1,100]); AddEdgeOrbit(gamma,[100,1]);
gap> hexad:=First(Orbits(h,[1..22]),i->Length(i)=6);
[ 1, 7, 18, 20, 12, 15 ]
gap> for i in hexad do AddEdgeOrbit(gamma,[i,23]);
> AddEdgeOrbit(gamma,[23,i]);
> od;
gap> Adjacency(gamma,23);
[ 1, 7, 12, 15, 18, 20 ]
gap> stab:=Stabilizer(diag,23);;
gap> orbs:=Orbits(stab,[24..99]);;
gap> orbreps:=List(orbs,i->i[1]);
[ 24, 45 ]
gap> rep1:=orbreps[1];
24
gap> Adjacency(gamma,rep1);
[ 1, 8, 9, 10, 13, 18 ]
gap> Intersection(hexad,Adjacency(gamma,rep1));
[ 1, 18 ]
gap> rep2:=orbreps[2];
45
gap> Adjacency(gamma,rep2);
[ 2, 6, 10, 11, 13, 21 ]
gap> Intersection(hexad,Adjacency(gamma,rep2));
[  ]
gap> AddEdgeOrbit(gamma,[23,rep2]); AddEdgeOrbit(gamma,[rep2,23]);
gap> IsSimpleGraph(gamma);
true
gap> Adjacency(gamma,23);
[ 1, 7, 12, 15, 18, 20, 45, 47, 51, 54, 57, 59, 63, 64, 66, 70, 75, 76, 79, 
  82, 85, 87 ]
gap> IsDistanceRegular(gamma);
true
gap> aug:=AutGroupGraph(gamma);
<permutation group with 11 generators>
gap> Size(aug);
88704000
gap> small := SmallGeneratingSet(aug);;
gap> Length(small);
2
gap> aug2 := Group(small);
<permutation group with 2 generators>
gap> DisplayCompositionSeries(aug2);
G (2 gens, size 88704000)
 | Z(2)
S (2 gens, size 44352000)
 | HS
1 (0 gens, size 1)
gap> hs:=DerivedSubgroup(aug2);
<permutation group of size 44352000 with 2 generators>
gap> LoadPackage("ctbllib");
true
gap> ct:=CharacterTable("U3(5)");ct2:=CharacterTable("U3(5).2");
CharacterTable( "U3(5)" )
CharacterTable( "U3(5).2" )
gap> cths:=CharacterTable("hs");
CharacterTable( "HS" )
gap> OrdersClassRepresentatives(ct);
[ 1, 2, 3, 4, 5, 5, 5, 5, 6, 7, 7, 8, 8, 10 ]
gap> OrdersClassRepresentatives(ct2);
[ 1, 2, 3, 4, 5, 5, 5, 6, 7, 8, 10, 2, 4, 6, 8, 10, 12, 20, 20 ]
gap> ordhs:= OrdersClassRepresentatives(cths);
[ 1, 2, 2, 3, 4, 4, 4, 5, 5, 5, 6, 6, 7, 8, 8, 8, 10, 10, 11, 11, 12, 15, 20, 
  20 ]
gap> pos:= Position(ordhs,12);
21
gap> SizesCentralizers(cths)[pos];
12
gap> repeat e1:=Random(hs);until OrderPerm(e1)=12;
gap> e2:=e1^6;;
gap> SizesConjugacyClasses(ct2)[2];
525
gap> PowerMap(cths,3)[PowerMap(cths,2)[21]];
2
gap> SizesConjugacyClasses(cths)[2];
5775
gap> count:=0;;
gap> repeat
> u:=Subgroup(hs,[e1,e2^Random(hs)]);
> count:=count+1;
> until Index(hs,u)=176;
gap> count;
13
gap> ophom:=ActionHomomorphism(hs,RightTransversal(hs,u),OnRight);;
gap> hsop:=Image(ophom);
<permutation group with 2 generators>
gap> IsPrimitive(hsop,[1..176]);
true
gap> dp:=DirectProduct(hs,hsop);;
gap> emb1:=Embedding(dp,1);;
gap> emb2:=Embedding(dp,2);;
gap> diag:=List(GeneratorsOfGroup(hs), 
> i->Image(emb1,i)*Image(emb2,Image(ophom,i)));;
gap> diag:=Group(diag,());;SetName(diag,"hs-276");
gap> Size(diag);
44352000
gap> adj:=Adjacency(gamma,5);
[ 29, 31, 34, 35, 40, 44, 47, 55, 59, 61, 65, 70, 73, 75, 80, 81, 82, 83, 84, 
  85, 86, 100 ]
gap> t:=Stabilizer(diag,[5,adj[5]],OnSets);;
gap> sg := SmallGeneratingSet(t);;
gap> t := Subgroup(diag, sg);
<permutation group with 2 generators>
gap> DisplayCompositionSeries(t);
G (2 gens, size 40320)
 | Z(2)
S (15 gens, size 20160)
 | A(2,4) = L(3,4)
1 (0 gens, size 1)
gap> aus:=AutomorphismGroup(t);;
gap> Size(aus);
241920
gap> inner:=InnerAutomorphismsAutomorphismGroup(aus);
<group with 2 generators>
gap> Index(aus,inner);
6
gap> rt:=RightTransversal(aus,inner);;
gap> automs:=Filtered(rt,i->i^2 in inner and not i in inner);;
gap> Length(automs);
3
gap> List(automs,Order);
[ 2, 2, 2 ]
gap> List(automs,IsConjugatorAutomorphism);
[ false, true, false ]
gap> element:=ConjugatorInnerAutomorphism(automs[2]);;
gap> co3:=Group(Concatenation(GeneratorsOfGroup(diag),[element]),());
<permutation group with 3 generators>
gap> Size(co3);
495766656000
gap> DisplayCompositionSeries(co3);
G (3 gens, size 495766656000)
 | Co(3)
1 (0 gens, size 1)
gap> quit;
